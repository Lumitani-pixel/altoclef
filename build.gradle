plugins {
    id "fabric-loom" version "1.11-SNAPSHOT"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

version = "${project.mod_version}-1.21.10"
group = project.maven_group

repositories {
    mavenCentral()
    maven { url "https://maven.fabricmc.net/" }
    maven { url "https://jitpack.io" }
    maven { url "https://babbaj.github.io/maven/" }
    maven {
        name = "meteor-maven"
        url = uri("https://maven.meteordev.org/releases")
    }
    maven {
        name = "meteor-maven-snapshots"
        url = uri("https://maven.meteordev.org/snapshots")
    }
    flatDir { dirs '../baritone/dist' }
}

dependencies {
    // Minecraft + Yarn + Fabric Loader + Fabric API
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.mappings_version}"
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

    // MixinExtras
    include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:0.3.5")))

    // Jackson JSON
    implementation 'com.fasterxml.jackson.core:jackson-core:2.16.0'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.16.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.0'

    // Baritone Integration (if available)
    modImplementation "meteordevelopment:baritone:${project.minecraft_version}-SNAPSHOT"
    include "meteordevelopment:baritone:${project.minecraft_version}-SNAPSHOT"

    // Nether pathfinder (optional)
    implementation 'dev.babbaj:nether-pathfinder:1.5'

    // Log4J for logging
    implementation 'org.apache.logging.log4j:log4j-api:2.24.3'
    runtimeOnly 'org.apache.logging.log4j:log4j-core:2.24.3'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release = 21
}

shadowJar {
    configurations = [project.configurations.shadow]
}

remapJar {
    dependsOn shadowJar
    input.set shadowJar.archiveFile
}

processResources {
    inputs.property("version", project.version)
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(remapJar) { builtBy remapJar }
            artifact(sourcesJar) { builtBy remapSourcesJar }
        }
    }
}